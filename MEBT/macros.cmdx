! convert current into K, using a third order polynomial
! as from MADbuilder
I2K_MEBT_QUs_third(MagName,biro) : macro {
  ! the macro expects I_MagName, K_MagName and F_MagName to be defined
  ! eg. M1_010A_QIB -> I_M1_010A_QIB, K_M1_010A_QIB, F_M1_010A_QIB
  K1_MagName = ( 4.37087e-2
                +6.804791e-2   *abs(I_MagName)
                +1.898403e-5   *abs(I_MagName)^2
                -9.8237885E-08 *abs(I_MagName)^3) /biro *abs(F_MagName);
  if (I_MagName<0){ K1_MagName=-K1_MagName; };
};

I2K_MEBT_CIBH(MagName,biro): macro {
  ! from SSavazzi, relation for MADbuilder
  ! the macro expects IH_MagName, HKICK_MagName and FH_MagName to be defined
  ! eg. M1_005A_CIB -> IH_M1_005A_CIB, HKICK_M1_005A_CIB, FH_M1_005A_CIB
  ! Lmag=0.3315m, Lgeo=0.164m;
  !
  ! Magnetic Measurements doc, 2007, performed on prototype
  ! S:\Accelerating-System\Magnets\Magnetic Measurements\Frascati\MEBT\Documenti_misure_e_immagini\Correttori
  ! BLh[Tm]=-1.38 10-4 * I [A] + 1.53 10-5
  ! change sign: a negative vertical B-field (in both ref systems, i.e.
  !    measurements and MADX) kicks positively-charged particles towards x>0 in MADX
  HKICK_MagName :=-((-1.38E-4*IH_MagName + 1.53E-5)/biro)*abs(FH_MagName);
};

I2K_MEBT_CIBV(MagName,biro): macro {
  ! from SSavazzi, relation for MADbuilder
  ! the macro expects IV_MagName, VKICK_MagName and FV_MagName to be defined
  ! eg. M1_005A_CIB -> IV_M1_005A_CIB, VKICK_M1_005A_CIB, FV_M1_005A_CIB
  ! Lmag=0.3315m, Lgeo=0.164m;
  !
  ! Magnetic Measurements doc, 2007, performed on prototype
  ! S:\Accelerating-System\Magnets\Magnetic Measurements\Frascati\MEBT\Documenti_misure_e_immagini\Correttori
  ! BLh[Tm]=-1.38 10-4 * I [A] + 1.62 10-5
  ! change sign: a negative horizontal B-field in measurements ref sys
  !    corresponds to a positive horizontal B-field in MADX, which kicks
  !    positively-charged particles towards y>0
  VKICK_MagName :=-((-1.38E-4*IV_MagName + 1.62E-5)/biro)*abs(FV_MagName);
};

save_strengths_particular(): macro {
   print, TEXT="";
   print, TEXT="! quadrupole strengths [rad/m^2]";
   value, K1_M1_010A_QIB;
   value, K1_M1_012A_QIB;
   value, K1_M1_014A_QIB;
   value, K1_M1_016A_QIB;
   value, K1_M2_009A_QIB;
   value, K1_M3_008A_QIB;
   value, K1_M3_010A_QIB;
   value, K1_M3_019A_QIB;
   value, K1_M3_021A_QIB;
   print, TEXT="";
   print, TEXT="! corrector kicks [rad]";
   value, HKICK_M1_005A_CIB ;
   value, VKICK_M1_005A_CIB ;
   value, HKICK_M2_004A_CIB ;
   value, VKICK_M2_004A_CIB ;
   value, HKICK_M2_012A_CIB ;
   value, VKICK_M2_012A_CIB ;
   value, HKICK_M3_006A_CIB ;
   value, VKICK_M3_006A_CIB ;
   value, HKICK_M3_014A_CIB ;
   value, VKICK_M3_014A_CIB ;
   value, HKICK_M3_026A_CIB ;
   value, VKICK_M3_026A_CIB ;
   value, HKICK_M3_030B_CIB ;
   value, VKICK_M3_030B_CIB ;
};

compute_RM_allCurrents(myMagName,myKName,myIName,myFName,myProperty,myI2K,fileName): macro={
   REMOVEFILE, FILE=fileName;
   exec, writeRMHeader(fileName);
   
   ! loop through current values
   currI=Imin;
   while (currI<=Imax) {

      myIName=currI;
      myProperty_orig=myMagName->myProperty;
      myFName=1.0;
      exec, myI2K(myMagName,Brho);
      ! value, myMagName->myProperty;
      myMagName->myProperty=myKName;
      ! value, myMagName->myProperty;
      
      use,sequence=MUXL;
      select,flag=twiss,clear;
      twiss, beta0=initial;

      ! save data
      kWrite=myKName;
      exec, writeRMObservations(fileName);

      ! reset kick to original value
      ! value, myMagName->myProperty;
      myMagName->myProperty=myProperty_orig;
      ! value, myMagName->myProperty;

      ! get ready for new point
      currI=currI+Istep;
      
  };
};
